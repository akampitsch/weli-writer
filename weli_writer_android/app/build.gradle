apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "de.mannodermaus.android-junit5"
apply plugin: "kotlin-kapt"
apply plugin: "com.google.dagger.hilt.android"
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: "com.google.gms.google-services"
apply plugin: "com.google.firebase.crashlytics"

android {
    compileSdkVersion 33
    buildToolsVersion "33.0.2"

    namespace "com.szoldapps.weli.writer"

    defaultConfig {
        applicationId "com.szoldapps.weli.writer"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 6
        versionName "1.0-alpha.6"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        viewBinding true
        compose true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    composeOptions {
        kotlinCompilerExtensionVersion "$composeVersion"
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    // Kotlin
    implementation(libs.androidx.core.ktx)
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.fragment:fragment-ktx:1.5.7"
    def androidXLifeCycleVersion = "2.4.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$androidXLifeCycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$androidXLifeCycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidXLifeCycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidXLifeCycleVersion"

    // hilt
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    kapt "com.google.dagger:hilt-android-compiler:$hiltVersion"

    // navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"
    androidTestImplementation "androidx.navigation:navigation-testing:$navVersion"

    // room
    def roomVersion = "2.5.1"
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    testImplementation "androidx.room:room-testing:$roomVersion"

    // ThreeTenAbp
    implementation "com.jakewharton.threetenabp:threetenabp:1.4.6"
    testImplementation("org.threeten:threetenbp:1.4.6") {
        exclude module: "com.jakewharton.threetenabp:threetenabp:1.4.6"
    }

    // Timer
    implementation "com.jakewharton.timber:timber:4.7.1"

    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"

    // jUnit 5
    def jUnit5version = "5.7.0"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$jUnit5version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$jUnit5version"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$jUnit5version"

    // Coroutines test
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.1"

    // compose
    implementation platform("androidx.compose:compose-bom:2023.04.01")
    // Tooling support (Previews, etc.)
    implementation "androidx.compose.ui:ui-tooling"
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation "androidx.compose.foundation:foundation"
    // Material Design
    implementation "androidx.compose.material:material"
    // Material design icons
    implementation "androidx.compose.material:material-icons-core"
    implementation "androidx.compose.material:material-icons-extended"
    // Integration with activities
    implementation "androidx.activity:activity-compose:1.7.2"
    // Integration with ViewModels
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose"
    implementation "androidx.lifecycle:lifecycle-runtime-compose"
    // Integration with observables
    implementation "androidx.compose.runtime:runtime-livedata"
    implementation "androidx.compose.runtime:runtime-rxjava2"
    // When using a MDC theme
    implementation "com.google.android.material:compose-theme-adapter:1.2.1"
    // UI Tests
    androidTestImplementation "androidx.compose.ui:ui-test-junit4"

    // Firebase
    implementation platform("com.google.firebase:firebase-bom:29.0.1")
    implementation "com.google.firebase:firebase-crashlytics-ktx"
    implementation "com.google.firebase:firebase-analytics-ktx"

}
